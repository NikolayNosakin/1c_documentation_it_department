///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Предмет") Тогда 
		Объект.Предмет = Параметры.Предмет;
		Объект.ПредставлениеПредмета = ОбщегоНазначения.ПредметСтрокой(Объект.Предмет);
	КонецЕсли;
	
	Элементы.Предмет.Заголовок = Объект.ПредставлениеПредмета;
	//Элементы.ГруппаПредмет.Видимость = ЗначениеЗаполнено(Объект.Предмет);
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Автор = Пользователи.ТекущийПользователь();
		ФорматированныйТекст = Параметры.ЗначениеКопирования.Содержание.Получить();
		
		Элементы.ДатаЗаметки.Заголовок = НСтр("ru = 'Не записано'")
	Иначе
		Элементы.ДатаЗаметки.Заголовок = НСтр("ru = 'Записано'") + ": " + Формат(Объект.ДатаИзменения, "ДЛФ=DDT");
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ФорматированныйТекст = ТекущийОбъект.Содержание.Получить();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Содержание = Новый ХранилищеЗначения(ФорматированныйТекст, Новый СжатиеДанных(9));
	
	ТекстHTML = "";
	Вложения = Новый Структура;
	ФорматированныйТекст.ПолучитьHTML(ТекстHTML, Вложения);
	
	ТекущийОбъект.ТекстСодержания = СтроковыеФункцииКлиентСервер.ИзвлечьТекстИзHTML(ТекстHTML);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Элементы.ДатаЗаметки.Заголовок = НСтр("ru = 'Записано'") + ": " + Формат(Объект.ДатаИзменения, "ДЛФ=DDT");
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОповеститьОбИзменении(Объект.Ссылка);
	Если ЗначениеЗаполнено(Объект.Предмет) Тогда
		ОповеститьОбИзменении(Объект.Предмет);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредметНажатие(Элемент)
	ПоказатьЗначение(,Объект.Предмет);
КонецПроцедуры

&НаКлиенте
Процедура АвторНажатие(Элемент)
	ПоказатьЗначение(,Объект.Автор);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимость()
	Элементы.Автор.Заголовок = Объект.Автор;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФайл(Команда)
	Если НЕ Параметры.Ключ.Пустая() Тогда		
		ТипыФайлаФорматированногоДокумента = Новый СписокЗначений;
		ТипыФайлаФорматированногоДокумента.Добавить("PDF");
		ТипыФайлаФорматированногоДокумента.Добавить("HTML");
		ТипыФайлаФорматированногоДокумента.Добавить("TXT");
		ТипыФайлаФорматированногоДокумента.Добавить("ANSITXT");		
		ОповещениеПослеВыбораЭлемента = Новый ОписаниеОповещения("ВыборТипаФайлаСохранение", ЭтотОбъект);	
		ТипыФайлаФорматированногоДокумента.ПоказатьВыборЭлемента(ОповещениеПослеВыбораЭлемента,"Выберите тип сохраняемого файла");		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаСохранение(ВыбранныеФайлы, ПараметрыСохранения) Экспорт	
	Если ВыбранныеФайлы <> Неопределено Тогда		
		ПутьКФайлу = ВыбранныеФайлы[0];
		#Если МобильныйКлиент Тогда
			ПутьКФайлу = ПутьКФайлу + ПараметрыСохранения.НазваниеФайла;	
		#КонецЕсли					
		ФорматированныйТекст.Записать(ПутьКФайлу, ТипФайлаФорматированногоДокумента[ПараметрыСохранения.Расширение]);
		ОбщегоНазначенияКлиент.СообщитьПользователю("Файл сохранен. " + ПутьКФайлу);
	КонецЕсли;	 	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТипаФайлаСохранение(Элемент, Параметр) Экспорт
	Если Элемент <> Неопределено Тогда		
		Расширение = Элемент.Значение;
		НазваниеФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Объект.Наименование) + "." + Расширение;	
		ПараметрыСохранения = Новый структура("НазваниеФайла,Расширение",НазваниеФайла,Расширение);
		#Если МобильныйКлиент Тогда
			ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		#Иначе
			ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
			ДиалогВыбораФайла.ПолноеИмяФайла = НазваниеФайла;
			ДиалогВыбораФайла.Расширение = Расширение;
		#КонецЕсли				
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаСохранение", ЭтотОбъект,ПараметрыСохранения);
		ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОписаниеОповещения, ДиалогВыбораФайла);						
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
